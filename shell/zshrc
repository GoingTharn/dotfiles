# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
(( ${+commands[direnv]} )) && emulate zsh -c "$(direnv export zsh)"

if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

(( ${+commands[direnv]} )) && emulate zsh -c "$(direnv hook zsh)"

# zsh_autocomplete fix
export ZSH_AUTOSUGGEST_COMPLETION_IGNORE="git *"

################################################################################
# Generic helper functions
################################################################################

# Returns whether the given command is executable or aliased.
_has() {
  return $(whence $1 >/dev/null)
}

# Prepend a directory to path, if it exists and isn't already in the path.
_prepend_to_path() {
  if [ -d $1 -a -z ${path[(r)$1]} ]; then
    path=($1 $path);
  fi
}

# Append a directory to path, if it exists and isn't already in the path.
_append_to_path() {
  if [ -d $1 -a -z ${path[(r)$1]} ]; then
    path=($path $1);
  fi
}

# These are the default OMZ plugins that we'll install.
plugins=(
  brew
  colored-man-pages
  direnv
  docker
  docker-compose
  fast-syntax-highlighting
  git
  golang
  history-substring-search
  nvm
  python
  virtualenv
  virtualenvwrapper
  vi-mode
 # zsh-autosuggestions
)

################################################################################
# Oh-My-ZSH prerequisite setup
################################################################################

# Set OMZ installation location.
ZSH=$HOME/.oh-my-zsh
DISABLE_UPDATE_PROMPT=true
# Set zsh theme to be powerlevel10k with patched fontawesome symbols.
POWERLEVEL9K_MODE='nerdfont-complete'
ZSH_THEME='powerlevel10k/powerlevel10k'

# Use a more universal theme for terminals without special font support.
# https://apple.stackexchange.com/a/118500
# https://stackoverflow.com/a/5203740/3344451
_p10k_incompatible_emulators=(
  Apple_Terminal
  vscode
)
if [[ -n "${_p10k_incompatible_emulators[(r)$TERM_PROGRAM]}" ]]; then
  ZSH_THEME='robbyrussell'
fi

################################################################################
# FZF setup
################################################################################
if _has fzf; then
  if _has fd; then
    # Use fd for fzf.
    FZF_DEFAULT_COMMAND='fd --type f --follow --hidden'
    FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    # Use fd for fzf directory search.
    FZF_ALT_C_COMMAND='fd --type d --color never'
  fi

  # Display source tree and file preview for CTRL-T and ALT-C.
  if _has tree; then
    # Show subdir tree for directories.
    FZF_ALT_C_OPTS="--preview '(exa --tree --color=always {} || tree -C {}) | head -200'"
  fi

  # Bind alt-j/k/d/u to moving the preview window for fzf.
  FZF_DEFAULT_OPTS="--bind alt-k:preview-up,alt-j:preview-down,alt-u:preview-page-up,alt-d:preview-page-down"

  # Show previews for files and directories.
  # Having `bat` or `highlight` (or any of the other binaries below) installed
  # enables syntax highlighting.
  FZF_CTRL_T_OPTS="--preview '(bat --style=numbers --color=always {} || highlight -O ansi -l {} || coderay {} || rougify {} || cat {}) 2> /dev/null | head -200'"

  # Some basic fzf-tab configs.
  plugins+=(fzf-tab)
  zstyle ':fzf-tab:complete:cd:*' fzf-preview '(exa --tree --color=always $realpath || tree -C $realpath) 2> /dev/null'
  zstyle ':completion:*:descriptions' format '[%d]'
  zstyle ':fzf-tab:*' switch-group ',' '.'
fi

################################################################################
# Editor setup
################################################################################

# Set Vim as the default editor.
export EDITOR="vim"
# Use Neovim instead of classic Vim (if available)
if _has nvim; then
  alias vim="nvim"
  alias vi="nvim"
  export EDITOR="nvim"
fi

################################################################################
# Tmux setup
################################################################################

# Use 256 color for tmux.
alias tmux="TERM=screen-256color-bce tmux"
# Attempt to take over existing sessions before creating a new tmux session.
TMUX_DEFAULT_SESSION="tmux"
alias t="tmux -u a -d -t ${TMUX_DEFAULT_SESSION} 2> /dev/null || tmux -u new -s ${TMUX_DEFAULT_SESSION}"
if [[ -z "$TMUX" ]]; then
  # Switch to xterm if we're in a tmux session.
  TERM="xterm-256color"
fi

################################################################################
# Set up other environment variables, aliases, and options
################################################################################

# Make sure we're using UTF-8.
export LANG=en_US.UTF-8
export LC_CTYPE=en_US.UTF-8

export RIPGREP_CONFIG_PATH="$HOME/.ripgreprc"
# Export theme for http://github.com/sharkdp/bat.
export BAT_THEME="Solarized (dark)"

# The bat executable may sometimes be called batcat on Debian/Ubuntu.
if _has batcat; then
  alias bat="batcat"
fi

# Set shell options.
setopt correct # enable spelling suggestions
setopt rmstarsilent # silence rm * confirmation

# Add common bin directories to path.
_prepend_to_path $HOME/.local/bin
_prepend_to_path $HOME/go/bin
_prepend_to_path /usr/bin
_prepend_to_path /usr/local/bin
_prepend_to_path /usr/local/go/bin
_prepend_to_path /usr/local/sbin

# Decrease delay in entering normal mode in shell.
# https://www.johnhawthorn.com/2012/09/vi-escape-delays/
KEYTIMEOUT=1

################################################################################
# Source Oh-My-ZSH.
################################################################################

# All theme and plugin configs must come beforehand.
# Sourcing this may have side-effects, so order matters.
# For the most part, it seems like bindkey gets overwritten.
# Hence they must be after the OMZ sourcing.
source $ZSH/oh-my-zsh.sh

################################################################################
# Actions with side-effects after sourcing Oh-My-ZSH
################################################################################

# Set key bindings.
bindkey -v # vi mode for shell
# Key bindings for history search.
bindkey '\e[3~' delete-char
bindkey '^R' history-incremental-search-backward
# Explicity bind home and end keys (in case of terminal compatibility issues)
bindkey "${terminfo[khome]}" beginning-of-line
bindkey "${terminfo[kend]}" end-of-line

# Alias `exa` as default `ls` command (if available).
# This must come after OMZ. (The library overwrites this alias.)
if _has exa; then
  alias ls="exa"
fi

# Source fzf scripts via via local installation.
# OMZ overwrites some of these scripts, so this must come afterwards.
fzf_paths=(
  "${HOME}/.local/share/nvim/site/pack/packer/start/fzf"
  "${HOME}/.vim/plugged/fzf"
)
for fzf_path in "${fzf_paths[@]}"; do
  if [ -d $fzf_path ]; then
    _prepend_to_path "${fzf_path}/bin"
    # Source fzf key bindings and auto-completion.
    source "${fzf_path}/shell/key-bindings.zsh"
    source "${fzf_path}/shell/completion.zsh"
    break
  fi
done

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# Source local zshrc configs.
if [[ -f ~/.zshrc.local ]]; then
  source ~/.zshrc.local
fi

if [[ -f ~/.local/bin/z ]]; then
  source ~/.local/bin/z
fi


source ~/.zsh_aliases
fpath=(/home/jb/.oh-my-zsh/custom/completions /home/jb/.oh-my-zsh/custom/plugins/fzf-tab /home/jb/.oh-my-zsh/plugins/vi-mode /home/jb/.oh-my-zsh/plugins/virtualenvwrapper /home/jb/.oh-my-zsh/plugins/virtualenv /home/jb/.oh-my-zsh/plugins/python /home/jb/.oh-my-zsh/plugins/ripgrep /home/jb/.oh-my-zsh/plugins/nvm /home/jb/.oh-my-zsh/plugins/history-substring-search /home/jb/.oh-my-zsh/plugins/golang /home/jb/.oh-my-zsh/plugins/git /home/jb/.oh-my-zsh/plugins/fd /home/jb/.oh-my-zsh/custom/plugins/fast-syntax-highlighting /home/jb/.oh-my-zsh/plugins/docker-compose /home/jb/.oh-my-zsh/plugins/docker /home/jb/.oh-my-zsh/plugins/direnv /home/jb/.oh-my-zsh/plugins/colored-man-pages /home/jb/.oh-my-zsh/plugins/brew /home/jb/.oh-my-zsh/functions /home/jb/.oh-my-zsh/completions /home/jb/.oh-my-zsh/cache/completions /home/jb/.granted/zsh_autocomplete/granted/ /home/jb/.granted/zsh_autocomplete/assume/ /usr/local/share/zsh/site-functions /usr/share/zsh/vendor-functions /usr/share/zsh/vendor-completions /usr/share/zsh/functions/Calendar /usr/share/zsh/functions/Chpwd /usr/share/zsh/functions/Completion /usr/share/zsh/functions/Completion/AIX /usr/share/zsh/functions/Completion/BSD /usr/share/zsh/functions/Completion/Base /usr/share/zsh/functions/Completion/Cygwin /usr/share/zsh/functions/Completion/Darwin /usr/share/zsh/functions/Completion/Debian /usr/share/zsh/functions/Completion/Linux /usr/share/zsh/functions/Completion/Mandriva /usr/share/zsh/functions/Completion/Redhat /usr/share/zsh/functions/Completion/Solaris /usr/share/zsh/functions/Completion/Unix /usr/share/zsh/functions/Completion/X /usr/share/zsh/functions/Completion/Zsh /usr/share/zsh/functions/Completion/openSUSE /usr/share/zsh/functions/Exceptions /usr/share/zsh/functions/MIME /usr/share/zsh/functions/Math /usr/share/zsh/functions/Misc /usr/share/zsh/functions/Newuser /usr/share/zsh/functions/Prompts /usr/share/zsh/functions/TCP /usr/share/zsh/functions/VCS_Info /usr/share/zsh/functions/VCS_Info/Backends /usr/share/zsh/functions/Zftp /usr/share/zsh/functions/Zle /home/jb/.oh-my-zsh/custom/plugins/fzf-tab/lib)
export PYENV_ROOT="$HOME/.pyenv"
[[ -d $PYENV_ROOT/bin ]] && export PATH="$PYENV_ROOT/bin:$PATH"
eval "$(pyenv init - zsh)"
